#define TAK

piece base, hull, r1, r2, boats,
	  main_turret_1, main_sleeve_1, main_barrel_1, main_flare_1,
	  main_turret_2, main_sleeve_2, main_barrel_2, main_flare_2,
	  main_turret_3, main_sleeve_3, main_barrel_3, main_flare_3,
	  main_turret_4, main_sleeve_4, main_barrel_4, main_flare_4,
	  torp_turret, torp1, torp2, torp3, torp4,
	  aa4inch_turret, aa4inch_sleeve, aa4inch_barrel, aa4inch_flare,
	  pom_turret, pom_sleeve, pom_barrel1, pom_flare1, pom_barrel2, pom_flare2, pom_barrel3, pom_flare3, pom_barrel4, pom_flare4,
	  turret_20_1, sleeve_20_1, barrel_20_1, flare_20_1, turret_20_2, sleeve_20_2, barrel_20_2, flare_20_2,
	  turret_20_3, sleeve_20_3, barrel_20_3, flare_20_3, turret_20_4, sleeve_20_4, barrel_20_4, flare_20_4,
	  exhaust, wake1, wake2, wake3, wake4;

#define BASE_SMOKE_PERIOD	40
#define MIN_SMOKE_PERIOD	240

//********************Turret turn speeds
#define MAIN_TURRET_TURN		<17>
#define MAIN_TURRET_PITCH		<17>

#define AA4INCH_TURRET_TURN		<20>
#define AA4INCH_TURRET_PITCH	<20>

#define POM_TURRET_TURN			<25>
#define POM_TURRET_PITCH		<25>

#define AA20_TURRET_TURN		<30>
#define AA20_TURRET_PITCH		<45>

#define TORP_TURRET_TURN		<10>

//*********************Recoil
#define MAIN_GUN_RECOIL			[1]
#define MAIN_GUN_RECOIL_SPEED	[10]
#define MAIN_GUN_BACK_SPEED		[2]

#define AA4INCH_GUN_RECOIL			[1]
#define AA4INCH_GUN_RECOIL_SPEED	[10]
#define AA4INCH_GUN_BACK_SPEED		[2]

#define POM_GUN_RECOIL			[0.4]
#define POM_GUN_RECOIL_SPEED	[10]
#define POM_GUN_BACK_SPEED		[2]

#define AA20_GUN_RECOIL			[0.3]
#define AA20_GUN_RECOIL_SPEED	[10]
#define AA20_GUN_BACK_SPEED		[3]

//*********************Death anim
#define HULL_LIST_SPEED			<10>
#define HULL_LIST_ANGLE			<2>

//********************Engine smoke
#define ENGINE_SMOKE_PIECE	exhaust
#define ENGINE_SMOKE_PERIOD	200

#define RESTORE_PERIOD		2500

#define SIG_DEATH			1
#define SIG_MOVE			2
#define SIG_AIM1			4
#define SIG_AIM2			8
#define SIG_AIM3			16
#define SIG_AIM4			32
#define SIG_AIM5			64
#define SIG_AIM6			128
#define SIG_AIM7			256
#define SIG_AIM8			512
#define SIG_AIM9			1024
#define SIG_AIM10			2048
#define SIG_AIM11			4096

#define ALL_WEAPON_SIGS		SIG_AIM1|SIG_AIM2|SIG_AIM3|SIG_AIM4|SIG_AIM5|SIG_AIM6|SIG_AIM7|SIG_AIM8|SIG_AIM9|SIG_AIM10|SIG_AIM11

#define SMOKEPIECE	hull

#include "exptype.h"
#include "sfxtype.h"
#include "damageSmoke.h"
static-var	CurrentSpeed, bMoving, pom_flare, torp_flare,
			oldXZ, oldHeading;

WorkingLoop()
{
	while(TRUE)
	{
		if(bMoving)
		{
			// engine smoke comes here. And engine sound.
			emit-sfx SFXTYPE_BLACKSMOKE from ENGINE_SMOKE_PIECE;
			sleep ENGINE_SMOKE_PERIOD;
		}
		sleep 500;
	}
}

InitTurrets()
//Pre-position turrets
{
	turn main_turret_3 to y-axis <180> now;
	turn main_turret_4 to y-axis <180> now;
	turn aa4inch_turret to y-axis <180> now;
	turn torp_turret to y-axis <180> now;
	turn pom_turret to y-axis <180> now;
	turn turret_20_3 to y-axis <180> now;
	turn turret_20_4 to y-axis <180> now;
	pom_flare=1;
	torp_flare=1;
}

WaitForConstructionToFinish()
{
	while(get BUILD_PERCENT_LEFT)
	{
		sleep 150;
	}
}

Create()
{
	bMoving = FALSE;
	call-script InitTurrets();
	call-script WaitForConstructionToFinish();
	start-script DamageSmoke();
	start-script WorkingLoop();
}

SweetSpot(piecenum)
{
	piecenum = hull;
}

EmitWakes()
{
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		emit-sfx 2 from wake3;
		emit-sfx 2 from wake4;
		sleep 300;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	// We don't want this to trigger on VERY short stops
	sleep 300;
	bMoving = FALSE;
}

StopCatcher()
{
	var newXZ, newHeading;
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while(TRUE)
	{
		sleep 300;
		newXZ = get PIECE_XZ(base);
		newHeading = get HEADING;
		if((oldXZ == newXZ) && (oldHeading == newHeading))
		{
			call-script StopMoving();
		}
		oldXZ = newXZ;
		oldHeading = newHeading;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	start-script EmitWakes();
	start-script StopCatcher();
	bMoving = TRUE;
}

RestoreTurret()
{
	set-signal-mask SIG_DEATH|ALL_WEAPON_SIGS;
	sleep RESTORE_PERIOD;

	turn main_turret_1 to y-axis <0> speed MAIN_TURRET_TURN;
	turn main_sleeve_1 to x-axis <0> speed MAIN_TURRET_PITCH;
	turn main_turret_2 to y-axis <0> speed MAIN_TURRET_TURN;
	turn main_sleeve_2 to x-axis <0> speed MAIN_TURRET_PITCH;
	turn main_turret_3 to y-axis <180> speed MAIN_TURRET_TURN;
	turn main_sleeve_3 to x-axis <0> speed MAIN_TURRET_PITCH;
	turn main_turret_4 to y-axis <180> speed MAIN_TURRET_TURN;
	turn main_sleeve_4 to x-axis <0> speed MAIN_TURRET_PITCH;

	turn aa4inch_turret to y-axis <180> speed AA4INCH_TURRET_TURN;
	turn aa4inch_sleeve to x-axis <0> speed AA4INCH_TURRET_PITCH;

	turn pom_turret to y-axis <180> speed POM_TURRET_TURN;
	turn pom_sleeve to x-axis <0> speed POM_TURRET_PITCH;

	turn turret_20_1 to y-axis <0> speed AA20_TURRET_TURN;
	turn sleeve_20_1 to x-axis <0> speed AA20_TURRET_PITCH;
	turn turret_20_2 to y-axis <0> speed AA20_TURRET_TURN;
	turn sleeve_20_2 to x-axis <0> speed AA20_TURRET_PITCH;
	turn turret_20_3 to y-axis <180> speed AA20_TURRET_TURN;
	turn sleeve_20_3 to x-axis <0> speed AA20_TURRET_PITCH;
	turn turret_20_4 to y-axis <180> speed AA20_TURRET_TURN;
	turn sleeve_20_4 to x-axis <0> speed AA20_TURRET_PITCH;

	turn torp_turret to y-axis <180> speed TORP_TURRET_TURN;
}

QueryWeapon1(piecenum)
{
	piecenum = main_flare_1;
}

AimFromWeapon1(piecenum)
{
	piecenum = main_turret_1;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1|SIG_DEATH;
	turn main_turret_1 to y-axis heading speed MAIN_TURRET_TURN;
	turn main_sleeve_1 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn main_turret_1 around y-axis;
	wait-for-turn main_sleeve_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon1()
{
	show main_flare_1;
	move main_barrel_1 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move main_barrel_1 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide main_flare_1;
}

QueryWeapon2(piecenum)
{
	piecenum = main_flare_2;
}

AimFromWeapon2(piecenum)
{
	piecenum = main_turret_2;
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2|SIG_DEATH;
	turn main_turret_2 to y-axis heading speed MAIN_TURRET_TURN;
	turn main_sleeve_2 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn main_turret_2 around y-axis;
	wait-for-turn main_sleeve_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon2()
{
	show main_flare_2;
	move main_barrel_2 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move main_barrel_2 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide main_flare_2;
}

QueryWeapon3(piecenum)
{
	piecenum = main_flare_3;
}

AimFromWeapon3(piecenum)
{
	piecenum = main_turret_3;
}

AimWeapon3(heading, pitch)
{
	signal SIG_AIM3;
	set-signal-mask SIG_AIM3|SIG_DEATH;
	turn main_turret_3 to y-axis heading speed MAIN_TURRET_TURN;
	turn main_sleeve_3 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn main_turret_3 around y-axis;
	wait-for-turn main_sleeve_3 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon3()
{
	show main_flare_3;
	move main_barrel_3 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move main_barrel_3 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide main_flare_3;
}

QueryWeapon4(piecenum)
{
	piecenum = main_flare_4;
}

AimFromWeapon4(piecenum)
{
	piecenum = main_turret_4;
}

AimWeapon4(heading, pitch)
{
	signal SIG_AIM4;
	set-signal-mask SIG_AIM4|SIG_DEATH;
	turn main_turret_4 to y-axis heading speed MAIN_TURRET_TURN;
	turn main_sleeve_4 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn main_turret_4 around y-axis;
	wait-for-turn main_sleeve_4 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon4()
{
	show main_flare_4;
	move main_barrel_4 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move main_barrel_4 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide main_flare_4;
}

QueryWeapon5(piecenum)
{
	piecenum = aa4inch_flare;
}

AimFromWeapon5(piecenum)
{
	piecenum = aa4inch_turret;
}

AimWeapon5(heading, pitch)
{
	signal SIG_AIM5;
	set-signal-mask SIG_AIM5|SIG_DEATH;
	turn aa4inch_turret to y-axis heading speed AA4INCH_TURRET_TURN;
	turn aa4inch_sleeve to x-axis (0-pitch) speed AA4INCH_TURRET_PITCH;
	wait-for-turn aa4inch_turret around y-axis;
	wait-for-turn aa4inch_sleeve around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon5()
{
	show aa4inch_flare;
	move aa4inch_barrel to z-axis (0-AA4INCH_GUN_RECOIL) speed AA4INCH_GUN_RECOIL_SPEED;
	sleep 200;
	move aa4inch_barrel to z-axis [0] speed AA4INCH_GUN_BACK_SPEED;
	hide aa4inch_flare;
}

QueryWeapon6(piecenum)
{
	if(pom_flare == 1)
	{
		piecenum = pom_flare1;
	}
	if(pom_flare == 2)
	{
		piecenum = pom_flare2;
	}
	if(pom_flare == 3)
	{
		piecenum = pom_flare3;
	}
	if(pom_flare == 4)
	{
		piecenum = pom_flare4;
	}
}

AimFromWeapon6(piecenum)
{
	piecenum = pom_turret;
}

AimWeapon6(heading, pitch)
{
	signal SIG_AIM6;
	set-signal-mask SIG_AIM6|SIG_DEATH;
	turn pom_turret to y-axis heading speed POM_TURRET_TURN;
	turn pom_sleeve to x-axis (0-pitch) speed POM_TURRET_PITCH;
	wait-for-turn pom_turret around y-axis;
	wait-for-turn pom_sleeve around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon6()
{
	if(pom_flare == 1)
	{
		show pom_flare1;
		move pom_barrel1 to z-axis (0-POM_GUN_RECOIL) speed POM_GUN_RECOIL_SPEED;
		sleep 200;
		move pom_barrel1 to z-axis [0] speed POM_GUN_BACK_SPEED;
		hide pom_flare1;
	}
	if(pom_flare == 2)
	{
		show pom_flare2;
		move pom_barrel2 to z-axis (0-POM_GUN_RECOIL) speed POM_GUN_RECOIL_SPEED;
		sleep 200;
		move pom_barrel2 to z-axis [0] speed POM_GUN_BACK_SPEED;
		hide pom_flare2;
	}
	if(pom_flare == 3)
	{
		show pom_flare3;
		move pom_barrel3 to z-axis (0-POM_GUN_RECOIL) speed POM_GUN_RECOIL_SPEED;
		sleep 200;
		move pom_barrel3 to z-axis [0] speed POM_GUN_BACK_SPEED;
		hide pom_flare3;
	}
	if(pom_flare == 4)
	{
		show pom_flare4;
		move pom_barrel4 to z-axis (0-POM_GUN_RECOIL) speed POM_GUN_RECOIL_SPEED;
		sleep 200;
		move pom_barrel4 to z-axis [0] speed POM_GUN_BACK_SPEED;
		hide pom_flare4;
	}
	pom_flare = pom_flare + 1;
	if(pom_flare>4)
	{
		pom_flare = 1;
	}
}

QueryWeapon7(piecenum)
{
	piecenum = flare_20_1;
}

AimFromWeapon7(piecenum)
{
	piecenum = turret_20_1;
}

AimWeapon7(heading, pitch)
{
	signal SIG_AIM7;
	set-signal-mask SIG_AIM7|SIG_DEATH;
	turn turret_20_1 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve_20_1 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret_20_1 around y-axis;
	wait-for-turn sleeve_20_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon7()
{
	show flare_20_1;
	move barrel_20_1 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_20_1 to z-axis [0] speed AA20_GUN_BACK_SPEED;
	hide flare_20_1;
}

QueryWeapon8(piecenum)
{
	piecenum = flare_20_2;
}

AimFromWeapon8(piecenum)
{
	piecenum = turret_20_2;
}

AimWeapon8(heading, pitch)
{
	signal SIG_AIM8;
	set-signal-mask SIG_AIM8|SIG_DEATH;
	turn turret_20_2 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve_20_2 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret_20_2 around y-axis;
	wait-for-turn sleeve_20_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon8()
{
	show flare_20_2;
	move barrel_20_2 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_20_2 to z-axis [0] speed AA20_GUN_BACK_SPEED;
	hide flare_20_2;
}

QueryWeapon9(piecenum)
{
	piecenum = flare_20_3;
}

AimFromWeapon9(piecenum)
{
	piecenum = turret_20_3;
}

AimWeapon9(heading, pitch)
{
	signal SIG_AIM9;
	set-signal-mask SIG_AIM9|SIG_DEATH;
	turn turret_20_3 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve_20_3 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret_20_3 around y-axis;
	wait-for-turn sleeve_20_3 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon9()
{
	show flare_20_3;
	move barrel_20_3 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_20_3 to z-axis [0] speed AA20_GUN_BACK_SPEED;
	hide flare_20_3;
}

QueryWeapon10(piecenum)
{
	piecenum = flare_20_4;
}

AimFromWeapon10(piecenum)
{
	piecenum = turret_20_4;
}

AimWeapon10(heading, pitch)
{
	signal SIG_AIM10;
	set-signal-mask SIG_AIM10|SIG_DEATH;
	turn turret_20_4 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve_20_4 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret_20_4 around y-axis;
	wait-for-turn sleeve_20_4 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon10()
{
	show flare_20_4;
	move barrel_20_4 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_20_4 to z-axis [0] speed AA20_GUN_BACK_SPEED;
	hide flare_20_4;
}

QueryWeapon11(piecenum)
{
	if(torp_flare == 1)
	{
		piecenum = torp1;
	}
	if(torp_flare == 2)
	{
		piecenum = torp2;
	}
	if(torp_flare == 3)
	{
		piecenum = torp3;
	}
	if(torp_flare == 4)
	{
		piecenum = torp4;
	}
}

AimFromWeapon11(piecenum)
{
	piecenum = torp_turret;
}

AimWeapon11(heading, pitch)
{
	signal SIG_AIM11;
	set-signal-mask SIG_AIM11|SIG_DEATH;
	turn torp_turret to y-axis heading speed TORP_TURRET_TURN;
	wait-for-turn torp_turret around y-axis;
	start-script RestoreTurret();
	return (TRUE);
}

Shot11(foo)
{
	torp_flare = torp_flare + 1;
	if(torp_flare > 4)
	{
		torp_flare = 1;
	}
}

Killed(severity, corpsetype)
{
	signal SIG_DEATH;
	if (severity<99)
	{
		corpsetype = 1;
		turn hull to x-axis (0-HULL_LIST_ANGLE) speed HULL_LIST_SPEED;
		wait-for-turn hull around x-axis;
		return (TRUE);
	}
	corpsetype = 2;
	explode hull type		SHATTER|BITMAP1;
	return (TRUE);
}