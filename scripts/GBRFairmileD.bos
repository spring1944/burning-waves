#define TAK

piece base, hull, h2, turret6_1, sleeve6_1, barrel6_1, flare6_1, turret6_2, sleeve6_2, barrel6_2, flare6_2,
	  turret20, sleeve20, barrel20_1, flare20_1, barrel20_2, flare20_2, turret05_1, guns05_1, flare05_1_1, flare05_1_2,
	  turret05_2, guns05_2, flare05_2_1, flare05_2_2, turret303_1, gun303_1, flare303_1, turret303_2, gun303_2, flare303_2,
	  torp1, torp2, torp3, torp4, wake1, wake2, wake3, wake4;

#define BASE_SMOKE_PERIOD	40
#define MIN_SMOKE_PERIOD	240

//********************Turret turn speeds
#define MAIN_TURRET_TURN		<30>
#define MAIN_TURRET_PITCH		<20>

#define AA20_TURRET_TURN		<45>
#define AA20_TURRET_PITCH		<45>

#define MG05_TURRET_TURN		<45>
#define MG05_TURRET_PITCH		<45>

#define MG303_TURRET_TURN		<45>
#define MG303_TURRET_PITCH		<45>

//*********************Recoil
#define MAIN_GUN_RECOIL			[1]
#define MAIN_GUN_RECOIL_SPEED	[10]
#define MAIN_GUN_BACK_SPEED		[4]

#define AA20_GUN_RECOIL			[0.4]
#define AA20_GUN_RECOIL_SPEED	[10]
#define AA20_GUN_BACK_SPEED		[2]

//*********************Death anim
#define HULL_LIST_SPEED		<30>
#define HULL_LIST_ANGLE_Z	<-15>
#define HULL_LIST_ANGLE_X	<20>

//*********************Cruise mode
#define CRUISE_MODE_ANGLE	<-4>
#define CRUISE_MODE_RAISE_SPEED	<1>
#define CRUISE_MODE_START_DIVIDER	4
#define CRUISE_MODE_START_MULTIPLIER	3
#define CRUISE_MODE_PERIOD	250

#define RESTORE_PERIOD		2500

#define SIG_DEATH			1
#define SIG_MOVE			2
#define SIG_AIM1			4
#define SIG_AIM2			8
#define SIG_AIM3			16
#define SIG_AIM4			32
#define SIG_AIM5			64
#define SIG_AIM6			128
#define SIG_AIM7			256
#define SIG_AIM8			512
#define SIG_AIM9			1024
#define SIG_AIM10			2048
#define SIG_AIM11			4096
#define SIG_AIM12			8192

#define SMOKEPIECE	hull

#include "exptype.h"
#include "sfxtype.h"
#include "damageSmoke.h"
static-var					MaxSpeed, CruiseTriggerSpeed, bMoving, flare05_1, flare05_2, is20mmAimed;

WorkingLoop()
{
	while(TRUE)
	{
		if(bMoving)
		{
			// engine smoke comes here. And engine sound.
			sleep 500;
		}
		sleep 500;
	}
}

InitTurrets()
//Pre-position turrets
{
	turn turret6_2 to y-axis <180> now;
	turn turret20 to y-axis <180> now;
}

WaitForConstructionToFinish()
{
	while(get BUILD_PERCENT_LEFT)
	{
		sleep 150;
	}
}

CruiseControl()
{
	var CurrentSpeed;
	set-signal-mask SIG_DEATH;
	while(TRUE)
	{
		CurrentSpeed = get CURRENT_SPEED;
		if(CurrentSpeed > CruiseTriggerSpeed)
		{
			turn hull to x-axis CRUISE_MODE_ANGLE speed CRUISE_MODE_RAISE_SPEED;		
		} else
		{
			turn hull to x-axis <0> speed CRUISE_MODE_RAISE_SPEED;
		}
		sleep CRUISE_MODE_PERIOD;
	}
}

Create()
{
	bMoving = FALSE;
	is20mmAimed = FALSE;
	flare05_1 = TRUE;
	flare05_2 = TRUE;
	MaxSpeed = get MAX_SPEED;
	CruiseTriggerSpeed = MaxSpeed * CRUISE_MODE_START_MULTIPLIER / CRUISE_MODE_START_DIVIDER;
	call-script InitTurrets();
	call-script WaitForConstructionToFinish();
	start-script DamageSmoke();
	start-script WorkingLoop();
	start-script CruiseControl();
}

SweetSpot(piecenum)
{
	piecenum = hull;
}

EmitWakes()
{
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		emit-sfx 2 from wake3;
		emit-sfx 2 from wake4;
		sleep 300;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	start-script EmitWakes();
	bMoving = TRUE;
}

StopMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	// We don't want this to trigger on VERY short stops
	sleep 300;
	bMoving = FALSE;
}

RestoreTurret()
{
	set-signal-mask SIG_AIM1|SIG_AIM2|SIG_AIM3|SIG_AIM4|SIG_AIM5|SIG_AIM6|SIG_AIM7|SIG_AIM8|SIG_AIM9|SIG_AIM10|SIG_AIM11|SIG_AIM12|SIG_DEATH;
	sleep RESTORE_PERIOD;

	turn turret6_1 to y-axis <0> speed MAIN_TURRET_TURN;
	turn sleeve6_1 to x-axis <0> speed MAIN_TURRET_PITCH;
	turn turret6_2 to y-axis <180> speed MAIN_TURRET_TURN;
	turn sleeve6_2 to x-axis <0> speed MAIN_TURRET_PITCH;
	
	turn turret20 to y-axis <180> speed AA20_TURRET_TURN;
	turn sleeve20 to x-axis <0> speed AA20_TURRET_PITCH;

	turn turret05_1 to y-axis <0> speed MG05_TURRET_TURN;
	turn guns05_1 to x-axis <0> speed MG05_TURRET_PITCH;
	turn turret05_2 to y-axis <0> speed MG05_TURRET_TURN;
	turn guns05_2 to x-axis <0> speed MG05_TURRET_PITCH;

	turn turret303_1 to y-axis <0> speed MG303_TURRET_TURN;
	turn gun303_1 to x-axis <0> speed MG303_TURRET_PITCH;
	turn turret303_2 to y-axis <0> speed MG303_TURRET_TURN;
	turn gun303_2 to x-axis <0> speed MG303_TURRET_PITCH;
}

QueryWeapon1(piecenum)
{
	piecenum = torp1;
}

AimFromWeapon1(piecenum)
{
	piecenum = torp1;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1|SIG_DEATH;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon1()
{
	sleep 200;
}

QueryWeapon2(piecenum)
{
	piecenum = torp2;
}

AimFromWeapon2(piecenum)
{
	piecenum = torp2;
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2|SIG_DEATH;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon2()
{
	sleep 200;
}

QueryWeapon3(piecenum)
{
	piecenum = torp3;
}

AimFromWeapon3(piecenum)
{
	piecenum = torp3;
}

AimWeapon3(heading, pitch)
{
	signal SIG_AIM3;
	set-signal-mask SIG_AIM3|SIG_DEATH;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon3()
{
	sleep 200;
}

QueryWeapon4(piecenum)
{
	piecenum = torp4;
}

AimFromWeapon4(piecenum)
{
	piecenum = torp4;
}

AimWeapon4(heading, pitch)
{
	signal SIG_AIM4;
	set-signal-mask SIG_AIM4|SIG_DEATH;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon4()
{
	sleep 200;
}

QueryWeapon5(piecenum)
{
	piecenum = flare6_1;
}

AimFromWeapon5(piecenum)
{
	piecenum = turret6_1;
}

AimWeapon5(heading, pitch)
{
	signal SIG_AIM5;
	set-signal-mask SIG_AIM5|SIG_DEATH;
	turn turret6_1 to y-axis heading speed MAIN_TURRET_TURN;
	turn sleeve6_1 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn turret6_1 around y-axis;
	wait-for-turn sleeve6_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon5()
{
	show flare6_1;
	move barrel6_1 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel6_1 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide flare6_1;
}

QueryWeapon6(piecenum)
{
	piecenum = flare6_2;
}

AimFromWeapon6(piecenum)
{
	piecenum = turret6_2;
}

AimWeapon6(heading, pitch)
{
	signal SIG_AIM6;
	set-signal-mask SIG_AIM6|SIG_DEATH;
	turn turret6_2 to y-axis heading speed MAIN_TURRET_TURN;
	turn sleeve6_2 to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn turret6_2 around y-axis;
	wait-for-turn sleeve6_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon6()
{
	show flare6_2;
	move barrel6_2 to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel6_2 to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide flare6_2;
}

QueryWeapon7(piecenum)
{
	piecenum = flare20_1;
}

AimFromWeapon7(piecenum)
{
	piecenum = turret20;
}

AimWeapon7(heading, pitch)
{
	signal SIG_AIM7;
	set-signal-mask SIG_AIM7|SIG_DEATH;
	is20mmAimed = FALSE;
	turn turret20 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve20 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret20 around y-axis;
	wait-for-turn sleeve20 around x-axis;
	start-script RestoreTurret();
	is20mmAimed = TRUE;
	return (TRUE);
}

FireWeapon7()
{
	show flare20_1;
	move barrel20_1 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel20_1 to z-axis [0] speed AA20_GUN_BACK_SPEED;
	hide flare20_1;
}

QueryWeapon8(piecenum)
{
	piecenum = flare20_2;
}

AimFromWeapon8(piecenum)
{
	piecenum = turret20;
}

AimWeapon8(heading, pitch)
{
	signal SIG_AIM7;
	set-signal-mask SIG_AIM7|SIG_DEATH;
	while(!is20mmAimed)
	{
		sleep 100;
	}
	return (TRUE);
}

FireWeapon8()
{
	show flare20_2;
	move barrel20_2 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel20_2 to z-axis [0] speed AA20_GUN_BACK_SPEED;
	hide flare20_2;
}

QueryWeapon9(piecenum)
{
	if(flare05_1)
	{
		piecenum = flare05_1_1;
	} else
	{
		piecenum = flare05_1_2;
	}
}

AimFromWeapon9(piecenum)
{
	piecenum = turret05_1;
}

AimWeapon9(heading, pitch)
{
	signal SIG_AIM9;
	set-signal-mask SIG_AIM9|SIG_DEATH;
	turn turret05_1 to y-axis heading speed MG05_TURRET_TURN;
	turn guns05_1 to x-axis (0-pitch) speed MG05_TURRET_PITCH;
	wait-for-turn turret05_1 around y-axis;
	wait-for-turn guns05_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon9()
{
	if (flare05_1)
	{
		show flare05_1_1;
		sleep 200;
		hide flare05_1_1;
	} else
	{
		show flare05_1_2;
		sleep 200;
		hide flare05_1_2;
	}
	flare05_1 = !flare05_1;
}

QueryWeapon10(piecenum)
{
	if(flare05_2)
	{
		piecenum = flare05_2_1;
	} else
	{
		piecenum = flare05_2_2;
	}
}

AimFromWeapon10(piecenum)
{
	piecenum = turret05_2;
}

AimWeapon10(heading, pitch)
{
	signal SIG_AIM10;
	set-signal-mask SIG_AIM10|SIG_DEATH;
	turn turret05_2 to y-axis heading speed MG05_TURRET_TURN;
	turn guns05_2 to x-axis (0-pitch) speed MG05_TURRET_PITCH;
	wait-for-turn turret05_2 around y-axis;
	wait-for-turn guns05_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon10()
{
	if (flare05_2)
	{
		show flare05_2_1;
		sleep 200;
		hide flare05_2_1;
	} else
	{
		show flare05_2_2;
		sleep 200;
		hide flare05_2_2;
	}
	flare05_2 = !flare05_2;
}

QueryWeapon11(piecenum)
{
	piecenum = flare303_1;
}

AimFromWeapon11(piecenum)
{
	piecenum = turret303_1;
}

AimWeapon11(heading, pitch)
{
	signal SIG_AIM11;
	set-signal-mask SIG_AIM11|SIG_DEATH;
	turn turret303_1 to y-axis heading speed MG303_TURRET_TURN;
	turn gun303_1 to x-axis (0-pitch) speed MG303_TURRET_PITCH;
	wait-for-turn turret303_1 around y-axis;
	wait-for-turn gun303_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon11()
{
	show flare303_1;
	sleep 200;
	hide flare303_1;
}

QueryWeapon12(piecenum)
{
	piecenum = flare303_2;
}

AimFromWeapon12(piecenum)
{
	piecenum = turret303_2;
}

AimWeapon12(heading, pitch)
{
	signal SIG_AIM12;
	set-signal-mask SIG_AIM12|SIG_DEATH;
	turn turret303_2 to y-axis heading speed MG303_TURRET_TURN;
	turn gun303_2 to x-axis (0-pitch) speed MG303_TURRET_PITCH;
	wait-for-turn turret303_2 around y-axis;
	wait-for-turn gun303_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon12()
{
	show flare303_2;
	sleep 200;
	hide flare303_2;
}

Killed(severity, corpsetype)
{
	signal SIG_DEATH;
	if (severity<99)
	{
		corpsetype = 1;
		turn hull to z-axis HULL_LIST_ANGLE_Z speed HULL_LIST_SPEED;
		turn hull to x-axis HULL_LIST_ANGLE_X speed HULL_LIST_SPEED;
		wait-for-turn hull around z-axis;
		wait-for-turn hull around x-axis;
		explode hull type		BITMAPONLY|BITMAP1;
		return (TRUE);
	}
	corpsetype = 2;
	explode hull type		SHATTER|BITMAP1;
	return (TRUE);
}