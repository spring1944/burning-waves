#define TAK

piece base, hull1, hull2, main_turret, main_sleeve, main_barrel, main_flare,
	  turret_37_1, sleeve_37_1, barrel_37_1, flare_37_1, turret_37_2, sleeve_37_2, barrel_37_2, flare_37_2,
	  turret_20_1, sleeve_20_1, barrel_20_1, flare_20_1, turret_20_2, sleeve_20_2, barrel_20_2, flare_20_2,
	  turret_20_3, sleeve_20_3, barrel_20_3, flare_20_3, turret_20_4, sleeve_20_4, barrel_20_4, flare_20_4,
	  wake1, wake2, wake3, wake4, exhaust;

#define BASE_SMOKE_PERIOD	40
#define MIN_SMOKE_PERIOD	240

//********************Turret turn speeds
#define MAIN_TURRET_TURN	<10>
#define MAIN_TURRET_PITCH	<15>

#define AA37_TURRET_TURN		<30>
#define AA37_TURRET_PITCH		<30>

#define AA20_TURRET_TURN		<45>
#define AA20_TURRET_PITCH		<45>

//*********************Recoil
#define MAIN_GUN_RECOIL			[0.8]
#define MAIN_GUN_RECOIL_SPEED	[10]
#define MAIN_GUN_BACK_SPEED		[1]

#define AA37_GUN_RECOIL			[0.4]
#define AA37_GUN_RECOIL_SPEED	[10]
#define AA37_GUN_BACK_SPEED		[0.2]

#define AA20_GUN_RECOIL			[0.4]
#define AA20_GUN_RECOIL_SPEED	[10]
#define AA20_GUN_BACK_SPEED		[0.5]

//*********************Death anim
#define HULL_LIST_SPEED		<10>
#define HULL_LIST_ANGLE		<90>

#define RESTORE_PERIOD		2500

#define SIG_MOVE			2
#define SIG_AIM1			4
#define SIG_AIM2			8
#define SIG_AIM3			16
#define SIG_AIM4			32
#define SIG_AIM5			64
#define SIG_AIM6			128
#define SIG_AIM7			256
#define SIG_DEATH			512

#define SMOKEPIECE	hull1

#include "exptype.h"
#include "sfxtype.h"
#include "damageSmoke.h"
static-var					CurrentSpeed, bMoving, QuadA, QuadB;

WorkingLoop()
{
	while(TRUE)
	{
		if(bMoving)
		{
			// engine smoke comes here. And engine sound.
			emit-sfx SFXTYPE_BLACKSMOKE from exhaust;
			sleep 150;
		}
		sleep 500;
	}
}

InitTurrets()
//Pre-position turrets
{
	turn turret_37_2 to y-axis <180> now;
	turn turret_20_3 to y-axis <180> now;
	turn turret_20_4 to y-axis <180> now;
}

WaitForConstructionToFinish()
{
	while(get BUILD_PERCENT_LEFT)
	{
		sleep 150;
	}
}

Create()
{
	bMoving = FALSE;
	call-script InitTurrets();
	call-script WaitForConstructionToFinish();
	start-script DamageSmoke();
	start-script WorkingLoop();
}

SweetSpot(piecenum)
{
	piecenum = hull1;
}

EmitWakes()
{
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		emit-sfx 2 from wake3;
		emit-sfx 2 from wake4;
		sleep 300;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	start-script EmitWakes();
	bMoving = TRUE;
}

StopMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	// We don't want this to trigger on VERY short stops
	sleep 300;
	bMoving = FALSE;
}

RestoreTurret()
{
	set-signal-mask SIG_AIM1|SIG_AIM2|SIG_AIM3|SIG_AIM4|SIG_AIM5|SIG_AIM6|SIG_AIM7|SIG_DEATH;
	sleep RESTORE_PERIOD;
	turn main_turret to y-axis <0> speed MAIN_TURRET_TURN;
	turn main_sleeve to x-axis <0> speed MAIN_TURRET_PITCH;

	turn turret_37_1 to y-axis <0> speed AA37_TURRET_TURN;
	turn sleeve_37_1 to x-axis <0> speed AA37_TURRET_PITCH;

	turn turret_37_2 to y-axis <0> speed AA37_TURRET_TURN;
	turn sleeve_37_2 to x-axis <0> speed AA37_TURRET_PITCH;

	turn turret_20_1 to y-axis <0> speed AA20_TURRET_TURN;
	turn sleeve_20_1 to x-axis <0> speed AA20_TURRET_PITCH;
	turn turret_20_2 to y-axis <0> speed AA20_TURRET_TURN;
	turn sleeve_20_2 to x-axis <0> speed AA20_TURRET_PITCH;
	turn turret_20_3 to y-axis <0> speed AA20_TURRET_TURN;
	turn sleeve_20_3 to x-axis <0> speed AA20_TURRET_PITCH;
	turn turret_20_4 to y-axis <0> speed AA20_TURRET_TURN;
	turn sleeve_20_4 to x-axis <0> speed AA20_TURRET_PITCH;
}

QueryWeapon1(piecenum)
{
	piecenum = main_flare;
}

AimFromWeapon1(piecenum)
{
	piecenum = main_turret;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1|SIG_DEATH;
	turn main_turret to y-axis heading speed MAIN_TURRET_TURN;
	turn main_sleeve to x-axis (0-pitch) speed MAIN_TURRET_PITCH;
	wait-for-turn main_turret around y-axis;
	wait-for-turn main_sleeve around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon1()
{
	show main_flare;
	move main_barrel to z-axis (0-MAIN_GUN_RECOIL) speed MAIN_GUN_RECOIL_SPEED;
	sleep 200;
	move main_barrel to z-axis [0] speed MAIN_GUN_BACK_SPEED;
	hide main_flare;
}

QueryWeapon2(piecenum)
{
	piecenum = flare_37_1;
}

AimFromWeapon2(piecenum)
{
	piecenum = turret_37_1;
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2|SIG_DEATH;
	turn turret_37_1 to y-axis heading speed AA37_TURRET_TURN;
	turn sleeve_37_1 to x-axis (0-pitch) speed AA37_TURRET_PITCH;
	wait-for-turn turret_37_1 around y-axis;
	wait-for-turn sleeve_37_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon2()
{
	show flare_37_1;
	move barrel_37_1 to z-axis (0-AA37_GUN_RECOIL) speed AA37_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_37_1 to z-axis 0 speed AA37_GUN_BACK_SPEED;
	hide flare_37_1;
}

QueryWeapon3(piecenum)
{
	piecenum = flare_37_2;
}

AimFromWeapon3(piecenum)
{
	piecenum = turret_37_2;
}

AimWeapon3(heading, pitch)
{
	signal SIG_AIM3;
	set-signal-mask SIG_AIM3|SIG_DEATH;
	turn turret_37_2 to y-axis heading speed AA37_TURRET_TURN;
	turn sleeve_37_2 to x-axis (0-pitch) speed AA37_TURRET_PITCH;
	wait-for-turn turret_37_2 around y-axis;
	wait-for-turn sleeve_37_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon3()
{
	show flare_37_2;
	move barrel_37_2 to z-axis (0-AA37_GUN_RECOIL) speed AA37_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_37_2 to z-axis 0 speed AA37_GUN_BACK_SPEED;
	hide flare_37_2;
}

QueryWeapon4(piecenum)
{
	piecenum = flare_20_1;
}

AimFromWeapon4(piecenum)
{
	piecenum = turret_20_1;
}

AimWeapon4(heading, pitch)
{
	signal SIG_AIM4;
	set-signal-mask SIG_AIM4|SIG_DEATH;
	turn turret_20_1 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve_20_1 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret_20_1 around y-axis;
	wait-for-turn sleeve_20_1 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon4()
{
	show flare_20_1;
	move barrel_20_1 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_20_1 to z-axis 0 speed AA20_GUN_BACK_SPEED;
	hide flare_20_1;
}

QueryWeapon5(piecenum)
{
	piecenum = flare_20_2;
}

AimFromWeapon5(piecenum)
{
	piecenum = turret_20_2;
}

AimWeapon5(heading, pitch)
{
	signal SIG_AIM5;
	set-signal-mask SIG_AIM5|SIG_DEATH;
	turn turret_20_2 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve_20_2 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret_20_2 around y-axis;
	wait-for-turn sleeve_20_2 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon5()
{
	show flare_20_2;
	move barrel_20_2 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_20_2 to z-axis 0 speed AA20_GUN_BACK_SPEED;
	hide flare_20_2;
}

QueryWeapon6(piecenum)
{
	piecenum = flare_20_3;
}

AimFromWeapon6(piecenum)
{
	piecenum = turret_20_3;
}

AimWeapon6(heading, pitch)
{
	signal SIG_AIM6;
	set-signal-mask SIG_AIM6|SIG_DEATH;
	turn turret_20_3 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve_20_3 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret_20_3 around y-axis;
	wait-for-turn sleeve_20_3 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon6()
{
	show flare_20_3;
	move barrel_20_3 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_20_3 to z-axis 0 speed AA20_GUN_BACK_SPEED;
	hide flare_20_3;
}

QueryWeapon7(piecenum)
{
	piecenum = flare_20_4;
}

AimFromWeapon7(piecenum)
{
	piecenum = turret_20_4;
}

AimWeapon7(heading, pitch)
{
	signal SIG_AIM7;
	set-signal-mask SIG_AIM7|SIG_DEATH;
	turn turret_20_4 to y-axis heading speed AA20_TURRET_TURN;
	turn sleeve_20_4 to x-axis (0-pitch) speed AA20_TURRET_PITCH;
	wait-for-turn turret_20_4 around y-axis;
	wait-for-turn sleeve_20_4 around x-axis;
	start-script RestoreTurret();
	return (TRUE);
}

FireWeapon7()
{
	show flare_20_4;
	move barrel_20_4 to z-axis (0-AA20_GUN_RECOIL) speed AA20_GUN_RECOIL_SPEED;
	sleep 200;
	move barrel_20_4 to z-axis 0 speed AA20_GUN_BACK_SPEED;
	hide flare_20_4;
}

Killed(severity, corpsetype)
{
	signal SIG_DEATH;
	if (severity<50)
	{
		corpsetype = 1;
		turn hull1 to z-axis HULL_LIST_ANGLE speed HULL_LIST_SPEED;
		wait-for-turn hull1 around z-axis;
		explode hull1 type		BITMAPONLY|BITMAP1;
		return (TRUE);
	}
	corpsetype = 2;
	explode hull1 type		SHATTER|BITMAP1;
	explode hull2 type		SHATTER|BITMAP1;
	explode main_turret type	FALL|EXPLODE_ON_HIT|FIRE;
	return (TRUE);
}