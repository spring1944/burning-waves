#define TAK

piece base, hull, ramp, turret, grabber, link,
	  mount1, sleeve1, gun1, flare1, mount2, sleeve2, gun2, flare2,
	  wake1, wake2, wake3;

#define BASE_SMOKE_PERIOD	40
#define MIN_SMOKE_PERIOD	240

//********************Guns
#define MG_TURN_SPEED		<45>
#define MG_PITCH_SPEED		<45>

//********************Ramp
#define RAMP_OPEN_ANGLE		<90>
#define RAMP_OPEN_SPEED		<30>

#define LOAD_DIST			[125]

#define SIG_DEATH			1
#define SIG_MOVE			2
#define SIG_LOAD			4
#define SIG_AIM1			8
#define SIG_AIM2			16

#define SMOKEPIECE	hull

//effects
#define placeholder_EFFECT01 1024+1
#define placeholder_EFFECT02 1024+2
#define placeholder_EFFECT03 1024+3
#define placeholder_EFFECT04 1024+4
#define placeholder_EFFECT05 1024+5
#define placeholder_EFFECT06 1024+6
#define MG_MUZZLEFLASH 1024+7
#define SMALL_MUZZLEFLASH 1024+8
#define SMALL_MUZZLEDUST 1024+9

// tracers
#define SMALL_TRACER 	2048+2

#include "exptype.h"
#include "sfxtype.h"
#include "damageSmoke.h"
static-var					MaxSpeed, bRampOpen, bMoving, bTooFar, boom_len, boom_heading, boom_pitch;

WorkingLoop()
{
	while(TRUE)
	{
		if(bMoving)
		{
			// engine smoke comes here. And engine sound.
			sleep 500;
		}
		sleep 500;
	}
}

WaitForConstructionToFinish()
{
	while(get BUILD_PERCENT_LEFT)
	{
		sleep 150;
	}
}

HideFlares()
{
	hide flare1;
	hide flare2;
}

Create()
{
	start-script HideFlares();
	bMoving = FALSE;
	MaxSpeed = get MAX_SPEED;
	call-script WaitForConstructionToFinish();
	start-script DamageSmoke();
	start-script WorkingLoop();
}

OpenRamp()
{
	var newSpeed;
	// we are opening the ramp, let's slow down (else we'll have the hold full of water)
	newSpeed = MaxSpeed/10;
	set MAX_SPEED to newSpeed;
	turn ramp to x-axis RAMP_OPEN_ANGLE speed RAMP_OPEN_SPEED;
	wait-for-turn ramp around x-axis;
	bRampOpen = TRUE;
	return;
}

CloseRamp()
{
	bRampOpen = FALSE;
	turn ramp to x-axis 0 speed RAMP_OPEN_SPEED;
	wait-for-turn ramp around x-axis;
	// now the ramp is closed, we can go full speed again
	set MAX_SPEED to MaxSpeed;
	return;
}

//Transport stuff
BoomCalc(posxz,posy)
{
	var dxz,dy,lenxz;

	dxz = get PIECE_XZ(turret) - posxz;
	dy = posy - get PIECE_Y(turret);
	lenxz = get XZ_HYPOT(dxz);

	boom_len = get HYPOT(lenxz,dy);
	boom_heading = get XZ_ATAN(dxz);
	boom_pitch = get ATAN(dy,lenxz);
	bTooFar = (boom_len > LOAD_DIST);
}

PositionLink(posxz,posy)
{
	turn turret to y-axis boom_heading now;
	turn grabber to x-axis 0-boom_pitch now;
	move grabber to z-axis boom_len now;
}

ReturnLink()
{
	turn turret to y-axis <0> now;
	turn grabber to x-axis <0> now;
	move grabber to z-axis [0] now;
}

TransportPickup(unitid)
{
	signal SIG_LOAD;
	set-signal-mask SIG_LOAD;
    call-script BoomCalc(get UNIT_XZ(unitid),get UNIT_Y(unitid));
	if( bTooFar )
	{
		return (FALSE);
	}
	call-script OpenRamp();
	while (!bRampOpen)
	{
		sleep 250;
	}
	set BUSY to 1;
	attach-unit unitid to (0-1);
	//sleep 500;
	sleep 50;
	set BUSY to 0;
	sleep 2500;
	start-script CloseRamp();
}

TransportDrop(unitid, position)
{
	signal SIG_LOAD;
	set-signal-mask SIG_LOAD;
    call-script BoomCalc(position,get GROUND_HEIGHT(position));
	if( bTooFar )
	{
		return (1);
	}
	call-script OpenRamp();
	while (!bRampOpen)
	{
		sleep 250;
	}
	set BUSY to 1;
	attach-unit unitid to link;
	call-script PositionLink();
	sleep 50;
	drop-unit unitid;
	call-script ReturnLink();
	//sleep 1000;
	sleep 50;
	set BUSY to 0;
	sleep 1500;
	start-script CloseRamp();
}

SweetSpot(piecenum)
{
	piecenum = hull;
}

EmitWakes()
{
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while( TRUE )
	{
		emit-sfx 2 from wake1;
		emit-sfx 2 from wake2;
		emit-sfx 2 from wake3;
		sleep 300;
	}
}

StopMoving()
{
	signal SIG_MOVE;
	set-signal-mask SIG_MOVE;
	// We don't want this to trigger on VERY short stops
	sleep 300;
	bMoving = FALSE;
}

StopCatcher()
{
	var newXZ, oldXZ, newHeading, oldHeading;
	set-signal-mask SIG_MOVE|SIG_DEATH;
	while(TRUE)
	{
		sleep 300;
		newXZ = get PIECE_XZ(base);
		newHeading = get HEADING;
		if((oldXZ == newXZ) && (oldHeading == newHeading))
		{
			call-script StopMoving();
		}
		oldXZ = newXZ;
		oldHeading = newHeading;
	}
}

StartMoving()
{
	signal SIG_MOVE;
	start-script EmitWakes();
	start-script StopCatcher();
	bMoving = TRUE;
}

RestoreTurrets()
{
	set-signal-mask SIG_DEATH|SIG_AIM1|SIG_AIM2;
	sleep 2500;
	turn mount1 to y-axis 0 speed MG_TURN_SPEED;
	turn mount2 to y-axis 0 speed MG_TURN_SPEED;
	turn sleeve1 to x-axis 0 speed MG_TURN_SPEED;
	turn sleeve2 to x-axis 0 speed MG_TURN_SPEED;
}

QueryWeapon1(piecenum)
{
	piecenum = flare1;
}

AimFromWeapon1(piecenum)
{
	piecenum = mount1;
}

AimWeapon1(heading, pitch)
{
	signal SIG_AIM1;
	set-signal-mask SIG_AIM1;
	turn mount1 to y-axis heading speed MG_TURN_SPEED;
	turn gun1 to x-axis (0-pitch) speed MG_PITCH_SPEED;
	wait-for-turn mount1 around y-axis;
	wait-for-turn gun1 around x-axis;
	start-script RestoreTurrets();
	return TRUE;
}

FireWeapon1()
{
/*	show flare1;
	sleep 200;
	hide flare1;*/
}

Shot1(foo)
{
	emit-sfx SMALL_TRACER from flare1;
	emit-sfx MG_MUZZLEFLASH from flare1;
}

QueryWeapon2(piecenum)
{
	piecenum = flare2;
}

AimFromWeapon2(piecenum)
{
	piecenum = mount2;
}

AimWeapon2(heading, pitch)
{
	signal SIG_AIM2;
	set-signal-mask SIG_AIM2;
	turn mount2 to y-axis heading speed MG_TURN_SPEED;
	turn gun2 to x-axis (0-pitch) speed MG_PITCH_SPEED;
	wait-for-turn mount2 around y-axis;
	wait-for-turn gun2 around x-axis;
	start-script RestoreTurrets();
	return TRUE;
}

FireWeapon2()
{
/*	show flare2;
	sleep 200;
	hide flare2;*/
}

Shot2(foo)
{
	emit-sfx SMALL_TRACER from flare2;
	emit-sfx MG_MUZZLEFLASH from flare2;
}

Killed(severity, corpsetype)
{
	signal SIG_DEATH;
	if (severity<50)
	{
		corpsetype = 1;
		return (TRUE);
	}
	corpsetype = 2;
	explode hull type		SHATTER|BITMAP1;
	return (TRUE);
}